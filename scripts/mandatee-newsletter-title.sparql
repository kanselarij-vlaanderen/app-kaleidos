# A person carrying a special mandate, such a "minister-president" or "viceminister-president",
# is modelled as 2 mandatees: 1 regular "minister" and 1 with the special title.
# Most tasks (agenda-item relationships ...) are executed in the capacity of the regular minister.
# When displaying the minister in relation to some subject however,
# it is preferred to always show the special mandate's title rather than the regular "minister".
#
# Below query adds a Kaleidos/Valvas-specific predicate to mandatees that contains the title (including name)
# of a mandatee in the way it is desired to show on the newsletter/valvas website.

PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX person: <http://www.w3.org/ns/person#>
PREFIX persoon: <https://data.vlaanderen.be/ns/persoon#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

INSERT {
  GRAPH ?graph {
    ?mandatee ext:nieuwsbriefTitel ?newsletterTitle.
  }
} WHERE {
    GRAPH ?graph {
      ?mandatee
          a mandaat:Mandataris ;
          mandaat:start ?start;
          org:holds ?mandate;
          mandaat:isBestuurlijkeAliasVan ?person .
      ?mandate org:role ?role .
      ?person
          a person:Person ;
          persoon:gebruikteVoornaam	?firstName ;
  	      foaf:familyName	?lastName .
      OPTIONAL {
          ?higherMandatee
              a mandaat:Mandataris ;
              mandaat:start ?start;
              org:holds ?higherMandate ;
              mandaat:isBestuurlijkeAliasVan ?person .
          ?higherMandate org:role ?higherRole .
          FILTER(?higherRole IN (
              <http://themis.vlaanderen.be/id/bestuursfunctie/5fed907ce6670526694a03de>, # MP
              <http://themis.vlaanderen.be/id/bestuursfunctie/5fed907ce6670526694a03df> #  VMP
          ))
      }
      BIND(IF(BOUND(?higherRole), ?higherRole, ?role) AS ?highestRole )

      VALUES (?highestRole ?newsletterTitlePrefix) {
          (<http://themis.vlaanderen.be/id/bestuursfunctie/5fed907ce6670526694a03e0> "Vlaams minister")
          (<http://themis.vlaanderen.be/id/bestuursfunctie/5fed907ce6670526694a03de> "minister-president")
          (<http://themis.vlaanderen.be/id/bestuursfunctie/5fed907ce6670526694a03df> "viceminister-president")
      }
      BIND(CONCAT(?newsletterTitlePrefix, " ", ?firstName, " ", ?lastName) AS ?newsletterTitle)
  }
  VALUES ?graph {
      <http://mu.semte.ch/graphs/public>
      <http://data.kanselarij.vlaanderen.be/id/group/kanselarij>
  }
}

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX dossier: <https://data.vlaanderen.be/ns/dossier#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX besluitvorming: <https://data.vlaanderen.be/ns/besluitvorming#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX pub: <http://mu.semte.ch/vocabularies/ext/publicatie/>

WITH <http://mu.semte.ch/graphs/organizations/kanselarij>
INSERT {
  ?besluitvormingsaangelegenheid besluitvorming:sluitingsdatum ?created .
}
WHERE {
    ?dossier a dossier:Dossier ;
             ext:isGearchiveerd "true"^^<http://mu.semte.ch/vocabularies/typed-literals/boolean> ;
             dossier:Dossier.isNeerslagVan ?besluitvormingsaangelegenheid .
    ?besluitvormingsaangelegenheid besluitvorming:openingsdatum ?created .
    FILTER NOT EXISTS { ?besluitvormingsaangelegenheid besluitvorming:sluitingsdatum ?anyDate . }
};

WITH <http://mu.semte.ch/graphs/organizations/kanselarij>
DELETE {
  ?dossier ext:isGearchiveerd ?isAchived .
}
WHERE {
    ?dossier a dossier:Dossier ;
             ext:isGearchiveerd ?isAchived .
};

WITH <http://mu.semte.ch/graphs/organizations/kanselarij>
DELETE {
  ?subcase ext:isProcedurestapGearchiveerd ?isAchived .
}
WHERE{
    ?subcase a dossier:Procedurestap ;
             ext:isProcedurestapGearchiveerd ?isAchived .
};

PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX pub: <http://mu.semte.ch/vocabularies/ext/publicatie/>
PREFIX publication-status: <http://themis.vlaanderen.be/id/concept/publicatie-status/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <http://schema.org/>
DELETE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s ?p ?o.
    }
}
INSERT {
     GRAPH <http://mu.semte.ch/graphs/public> {
         ?newS ?p ?o.
     }
 }
WHERE{
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s a pub:Publicatiestatus.
        ?s ?p ?o.
        BIND (
            URI(
                REPLACE(
                    STR(?s),
                    "http://kanselarij.vo.data.gift/id/concept/publicatie-statussen/",
                    "http://themis.vlaanderen.be/id/concept/publicatie-status/")
                )
            as ?newS
        )
    }
}
;
INSERT DATA {
  GRAPH <http://mu.semte.ch/graphs/public> {
     <http://themis.vlaanderen.be/id/concept-scheme/7115c8d0-a0fd-4314-b57c-4f758f7e6d86> a <http://www.w3.org/2004/02/skos/core#ConceptScheme>;
     <http://mu.semte.ch/vocabularies/core/uuid> "7115c8d0-a0fd-4314-b57c-4f758f7e6d86" ;
     <http://www.w3.org/2004/02/skos/core#prefLabel> "Publicatiestatussen"@nl .

    publication-status:fa62e050-3960-440d-bed9-1c3d3e9923a8 skos:inScheme <http://themis.vlaanderen.be/id/concept-scheme/7115c8d0-a0fd-4314-b57c-4f758f7e6d86> .
    publication-status:2f8dc814-bd91-4bcf-a823-baf1cdc42475 skos:inScheme <http://themis.vlaanderen.be/id/concept-scheme/7115c8d0-a0fd-4314-b57c-4f758f7e6d86> .
    publication-status:9b9b0b5e-45c8-11eb-b378-0242ac130002 skos:inScheme <http://themis.vlaanderen.be/id/concept-scheme/7115c8d0-a0fd-4314-b57c-4f758f7e6d86> .
    publication-status:bc294fde-45c8-11eb-b378-0242ac130002 skos:inScheme <http://themis.vlaanderen.be/id/concept-scheme/7115c8d0-a0fd-4314-b57c-4f758f7e6d86> .
  }
}
;
DELETE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s ext:priority ?prio.
    }
}
INSERT {
     GRAPH <http://mu.semte.ch/graphs/public> {
         ?s schema:position ?prio.
     }
 }
WHERE{
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s a pub:Publicatiestatus;
            ext:priority ?prio.
    }
}




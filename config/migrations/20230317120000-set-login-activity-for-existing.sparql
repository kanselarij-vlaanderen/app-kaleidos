PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/system/users> {
    ?loginActivity a ext:LoginActivity ;
      mu:uuid ?uuid ;
      prov:startedAtTime ?loginTime ;
      prov:wasAssociatedWith ?user .
  }
}
WHERE {
  GRAPH ?g {
    ?account a foaf:OnlineAccount .
    ?user a foaf:Person ;
      foaf:account ?account .
    OPTIONAL { ?user dct:created ?created . }
  }
  VALUES (?g) {
    (<http://mu.semte.ch/graphs/public>)
    (<http://mu.semte.ch/graphs/system/users>)
  }
  BIND(STRUUID() AS ?uuid)
  BIND(IRI(CONCAT('http://themis.vlaanderen.be/id/aanmeldingsactiviteit/', ?uuid)) AS ?loginActivity)
  BIND(COALESCE(?created,
    xsd:dateTime(
        CONCAT(
          STR(YEAR(NOW())), "-",
          STR(MONTH(NOW())), "-",
          STR(DAY(NOW())), "T",
          STR(hours(now())), ":",
          STR(minutes(now())), ":",
          STR(FLOOR(seconds(now()))), "Z"
        )
      )
    ) AS ?loginTime)
  FILTER NOT EXISTS {
    [ a ext:LoginActivity ] prov:wasAssociatedWith ?user .
  }
}

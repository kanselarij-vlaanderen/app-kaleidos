PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX besluitvorming: <http://data.vlaanderen.be/ns/besluitvorming#>
PREFIX dbpedia:  <http://dbpedia.org/ontology/>
PREFIX dossier: <https://data.vlaanderen.be/ns/dossier#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

INSERT {
    GRAPH ?g {
        ?c dossier:Dossier.bestaatUit ?doc .
    }
}
WHERE {
    GRAPH ?g {
        ?c a dossier:Dossier .
        ?sc a dbpedia:UnitOfWork . # Might be deprecated by the time this gets merged.
        ?c dossier:doorloopt ?sc .
        {
          ?sc ext:bevatDocumentversie ?doc .
        }
        UNION 
        {
          ?sc ^besluitvorming:vindtPlaatsTijdens /
              besluitvorming:genereertAgendapunt /
              ext:bevatAgendapuntDocumentversie ?doc . # Might be deprecated by the time this gets merged
        }
        ?doc a dossier:Stuk .
    }
}

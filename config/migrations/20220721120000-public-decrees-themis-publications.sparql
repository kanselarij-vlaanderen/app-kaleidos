PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX dossier: <https://data.vlaanderen.be/ns/dossier#>
PREFIX besluitvorming: <http://data.vlaanderen.be/ns/besluitvorming#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX adms: <http://www.w3.org/ns/adms#>

# This query will insert a new publication activity for each meeting.
# ! the "prov:startedAtTime" needs to be changed to a time in the near future to trigger themis/valvas publication

INSERT {
  GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
    ?newThemisPublicationURI a ext:ThemisPublicationActivity ;
      mu:uuid ?newThemisPublicationuuid ;
      generiek:geplandeStart "2022-09-03T12:00:00+02:00"^^xsd:dateTime ;
      adms:status <http://themis.vlaanderen.be/id/concept/vrijgave-status/5da73f0d-6605-493c-9c1c-0d3a71bf286a> ;
      ext:scope "newsitems" ;
      ext:scope "documents" ;
      prov:used ?meeting .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
    ?meeting a besluit:Vergaderactiviteit .
    {
      SELECT DISTINCT ?meeting (STRUUID() AS ?newThemisPublicationuuid) WHERE {
        GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
          ?document a dossier:Stuk .
          ?document dct:title ?title .
          ?document ext:toegangsniveauVoorDocumentVersie ?toegangsniveau .
          ?document ^besluitvorming:geagendeerdStuk/^dct:hasPart/besluitvorming:isAgendaVoor ?meeting .
          ?meeting besluit:geplandeStart ?plannedStart .
          FILTER ( ?plannedStart > xsd:dateTime("2016-09-08T00:00:00Z"))
          FILTER ( regex(?title, ".*DEC.") )
          FILTER ( ?toegangsniveau != <http://themis.vlaanderen.be/id/concept/toegangsniveau/c3de9c70-391e-4031-a85e-4b03433d6266>)
        }
      } GROUP BY ?meeting ORDER BY ?plannedStart
    }
    BIND(IRI(CONCAT("http://themis.vlaanderen.be/id/themis-publicatie-activiteit/", ?newThemisPublicationuuid)) AS ?newThemisPublicationURI)
  }
}

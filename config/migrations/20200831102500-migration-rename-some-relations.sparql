PREFIX  ext:  <http://mu.semte.ch/vocabularies/ext/>
PREFIX  dbpedia:  <http://dbpedia.org/ontology/>
PREFIX  besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX  besluitvorming:  <http://data.vlaanderen.be/ns/besluitvorming#>

# KAS-1422
# agendaitem
# besluitvorming:heeftBevoegdeVoorAgendapunt does not exist, making it ext: instead

DELETE {
  GRAPH ?g {
    ?agendaitem besluitvorming:heeftBevoegdeVoorAgendapunt ?agendaitemMandatee .
  } 
}
INSERT {
  GRAPH ?g {
    ?agendaitem ext:heeftBevoegdeVoorAgendapunt ?agendaitemMandatee .
  } 
}
WHERE {
  GRAPH ?g {
    ?agendaitem a besluit:Agendapunt .
    ?agendaitem besluitvorming:heeftBevoegdeVoorAgendapunt ?agendaitemMandatee .
  }
}

;

# ext:bevatAgendapuntDocumentversie is defined in oslo model as besluitvorming:geagendeerdStuk

DELETE {
  GRAPH ?g {
    ?agendaitem ext:bevatAgendapuntDocumentversie ?document .
  } 
}
INSERT {
  GRAPH ?g {
    ?agendaitem besluitvorming:geagendeerdStuk ?document .
  } 
}
WHERE {
  GRAPH ?g {
    ?agendaitem a besluit:Agendapunt .
    ?agendaitem ext:bevatAgendapuntDocumentversie ?document .
  }
}

;

# besluitvorming:formeelOK does not exist, making it ext: instead

DELETE {
  GRAPH ?g {
    ?agendaitem besluitvorming:formeelOK ?formallyOk .
  } 
}
INSERT {
  GRAPH ?g {
    ?agendaitem ext:formeelOK ?formallyOk .
  } 
}
WHERE {
  GRAPH ?g {
    ?agendaitem a besluit:Agendapunt .
    ?agendaitem besluitvorming:formeelOK ?formallyOk .
  }
}

;

# subcase
# besluitvorming:heeftBevoegde does not exist, making it ext: instead

DELETE {
  GRAPH ?g {
    ?subcase besluitvorming:heeftBevoegde ?subcaseMandatee .
  } 
}
INSERT {
  GRAPH ?g {
    ?subcase ext:heeftBevoegde ?subcaseMandatee .
  } 
}
WHERE {
  GRAPH ?g {
    ?subcase a dbpedia:UnitOfWork .
    ?subcase besluitvorming:heeftBevoegde ?subcaseMandatee .
  }
}


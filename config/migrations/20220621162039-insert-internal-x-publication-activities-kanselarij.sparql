PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX besluitvorming: <http://data.vlaanderen.be/ns/besluitvorming#>

# Only convert in kanselarij-graph

INSERT {
    GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
        ?decisionPub a ext:InternalDecisionPublicationActivity .
        ?decisionPub mu:uuid ?uuid .
        ?decisionPub ext:internalDecisionPublicationActivityUsed ?meeting .
        ?decisionPub prov:startedAtTime ?publicationTime .
    }
} WHERE {
    GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
        ?meeting a besluit:Vergaderactiviteit .
        ?meeting ext:releasedDecisions ?publicationTime .
    }
    BIND (STRUUID() AS ?uuid)
    BIND (URI(CONCAT("http://themis.vlaanderen.be/id/internebeslissingspublicatieactiviteit/", ?uuid)) AS ?decisionPub)
}

;

INSERT {
    GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
        ?documentPub a ext:InternalDocumentPublicationActivity .
        ?documentPub mu:uuid ?uuid .
        ?documentPub ext:internalDocumentPublicationActivityUsed ?meeting .
        ?documentPub prov:startedAtTime ?publicationTime .
    }
} WHERE {
    GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
        ?meeting a besluit:Vergaderactiviteit .
        ?meeting ext:releasedDocuments ?publicationTime .
    }
    BIND (STRUUID() AS ?uuid)
    BIND (URI(CONCAT("http://themis.vlaanderen.be/id/internedocumentspublicatieactiviteit/", ?uuid)) AS ?documentPub)
}

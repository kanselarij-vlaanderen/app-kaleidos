PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>
PREFIX dossier: <https://data.vlaanderen.be/ns/dossier#>
PREFIX besluitvor: <https://data.vlaanderen.be/ns/besluitvorming#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

DELETE {
  GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
    ?decisionmakingFlow ?dcmfP ?dcmfO .
   	?dcmfS ?dcmfP_ ?decisionmakingFlow .

    ?case ?cP ?cO .
    ?cS ?cP_ ?case .

    ?subcase ?scP ?scO .
    ?scS ?scP_ ?subcase .

    ?agendaActivity ?aaP ?aaO .
    ?aaS ?aaP_ ?agendaActivity .

    ?agendaitem schema:position ?agendaitemPosition .
    ?agendaitem ext:isGoedkeuringVanDeNotulen ?isApproval .
    ?otherAgendaitem schema:position ?otherAgendaitemPosition .
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
    ?agendaitem schema:position 1 .
    ?agendaitem ext:isGoedkeuringVanDeNotulen "true"^^<http://mu.semte.ch/vocabularies/typed-literals/boolean> .
    ?otherAgendaitem schema:position ?newOtherAgendaitemPosition .
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
    VALUES ?decisionmakingFlowId { "8D5686A8-411C-11ED-BB35-EE395168DCF7" "8C806F51-411C-11ED-BB35-EE395168DCF7" }
    ?decisionmakingFlow mu:uuid ?decisionmakingFlowId .

    ###
    # Stuff we need to delete
    ?case dossier:Dossier.isNeerslagVan ?decisionmakingFlow .
    ?decisionmakingFlow dossier:doorloopt ?subcase .
    ?agendaActivity besluitvor:vindtPlaatsTijdens ?subcase .

    OPTIONAL { ?decisionmakingFlow ?dcmfP ?dcmfO }
   	OPTIONAL { ?dcmfS ?dcmfP_ ?decisionmakingFlow }

    OPTIONAL { ?case ?cP ?cO }
    OPTIONAL { ?cS ?cP_ ?case }

    OPTIONAL { ?subcase ?scP ?scO }
    OPTIONAL { ?scS ?scP_ ?subcase }

    OPTIONAL { ?agendaActivity ?aaP ?aaO }
    OPTIONAL { ?aaS ?aaP_ ?agendaActivity }
    ###

    ?agendaActivity besluitvor:genereertAgendapunt ?agendaitem .
    ?agendaitem ext:isGoedkeuringVanDeNotulen ?isApproval .

    ###
    # Update other agendaitems' positions
    ?agenda dct:hasPart ?agendaitem .
    ?agenda dct:hasPart ?otherAgendaitem .
    ?otherAgendaitem dct:type <http://themis.vlaanderen.be/id/concept/agendapunt-type/dd47a8f8-3ad2-4d5a-8318-66fc02fe80fd> .
    ?agendaitem schema:position ?agendaitemPosition .
    ?otherAgendaitem schema:position ?otherAgendaitemPosition .
    FILTER (?otherAgendaitemPosition < ?agendaitemPosition)
    BIND(?otherAgendaitemPosition + 1 AS ?newOtherAgendaitemPosition)
    ###
  }
}

PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX dossier: <https://data.vlaanderen.be/ns/dossier#>
PREFIX besluitvorming: <http://data.vlaanderen.be/ns/besluitvorming#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX prov: <http://www.w3.org/ns/prov#>

DELETE {
  GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
    ?document ext:toegangsniveauVoorDocumentVersie ?toegangsniveau .
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
    ?document ext:toegangsniveauVoorDocumentVersie <http://themis.vlaanderen.be/id/concept/toegangsniveau/c3de9c70-391e-4031-a85e-4b03433d6266> .
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
    ?document a dossier:Stuk .
    ?document dct:title ?title .
    ?document ext:toegangsniveauVoorDocumentVersie ?toegangsniveau .
    ?document ^besluitvorming:geagendeerdStuk/^dct:hasPart/besluitvorming:isAgendaVoor ?meeting .
    ?meeting besluit:geplandeStart ?plannedStart .
    FILTER ( ?plannedStart > xsd:dateTime("2016-09-08T00:00:00Z"))
    FILTER ( regex(?title, ".*DEC.") )
    FILTER ( ?toegangsniveau != <http://themis.vlaanderen.be/id/concept/toegangsniveau/c3de9c70-391e-4031-a85e-4b03433d6266>)
  }
}
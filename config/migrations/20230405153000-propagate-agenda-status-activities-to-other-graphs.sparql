PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX besluitvorming: <http://data.vlaanderen.be/ns/besluitvorming#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX generiek: <http://data.vlaanderen.be/ns/generiek#>

INSERT {
  GRAPH ?g {
     ?agendaStatusActivity a ext:AgendaStatusActivity ;
                                           mu:uuid ?uuid ;
                                           prov:used ?agenda ;
                                           prov:startedAtTime ?modified ;
                                           generiek:bewerking ?agendaStatus .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
      ?agendaStatusActivity a ext:AgendaStatusActivity ;
                                           mu:uuid ?uuid ;
                                           prov:used ?agenda ;
                                           prov:startedAtTime ?modified ;
                                           generiek:bewerking ?agendaStatus .
  }
  VALUES (?g) {
    (<http://mu.semte.ch/graphs/organizations/intern-overheid>)
    (<http://mu.semte.ch/graphs/organizations/intern-regering>)
    (<http://mu.semte.ch/graphs/organizations/minister>)
  }
  GRAPH ?g {
     ?agenda a besluitvorming:Agenda .
  }
}
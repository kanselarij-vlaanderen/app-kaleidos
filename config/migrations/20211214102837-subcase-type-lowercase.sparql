PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

DELETE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s skos:prefLabel ?labelOld .
    }
} INSERT {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s skos:prefLabel ?labelNew .
    }
} WHERE {
    VALUES (?s ?labelOld) {
        (<http://example.com/step/343e05e1-24ca-4cfb-8708-048011c2b741> "In voorbereiding")
    }

    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s a ext:ProcedurestapType .
        ?s skos:prefLabel ?labelOld .
    }

    BIND (
    CONCAT(
        LCASE(SUBSTR(?labelOld, 1, 1)),
        SUBSTR(?labelOld, 2)
    ) AS ?labelNew)
}
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX besluitvorming: <http://data.vlaanderen.be/ns/besluitvorming#>

DELETE {
  GRAPH ?g {
    ?agendaItemTreatment besluitvorming:resultaat ?decisionResultCode .
  }
} WHERE {
  GRAPH ?g {
    ?agendaItemTreatment a besluit:BehandelingVanAgendapunt .
    ?agendaItemTreatment besluitvorming:heeftOnderwerp ?agendaitem .
    ?agendaitem a besluit:Agendapunt .
    ?agendaitem besluitvorming:aanmaakdatum ?datum .

    FILTER (?datum < xsd:dateTime("2019-10-01T00:00:00+01:00"))

    ?agendaItemTreatment besluitvorming:resultaat ?decisionResultCode .

    FILTER NOT EXISTS { ?next prov:wasRevisionOf ?agendaitem }
  }
}